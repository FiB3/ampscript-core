==========
AMPScript_Guide example 1
==========
%%[

var @s
var @o
var @p
var @m

OUTPUTLINE(concat("Strip leading zeroes from a string"))
set @s = "0000012345"
set @p = "^0*(\d+)$"
set @o = RegExMatch(@s, @p, 1)
OUTPUTLINE(concat("string: ", @s))
OUTPUTLINE(concat('pattern: "', @p, '"'))
OUTPUTLINE(concat("output: ", @o))

OUTPUTLINE(concat("Check for all digits, no match"))
set @s = "12345x"
set @p = "^\d*$"
set @o = RegExMatch(@s, @p, 0)
OUTPUTLINE(concat("string: ", @s))
OUTPUTLINE(concat('pattern: "', @p, '"'))
OUTPUTLINE(concat("output: ", @o))

OUTPUTLINE(concat("Check for all digits, match"))
set @s = "12345"
set @p = "^\d*$"
set @o = RegExMatch(@s, @p, 0)
OUTPUTLINE(concat("string: ", @s))
OUTPUTLINE(concat('pattern: "', @p, '"'))
OUTPUTLINE(concat("output: ", @o))

OUTPUTLINE(concat("Replace parenthetical text with space"))
set @s = "whee (whatever it is) whoop"
set @p = "\s\(.+\)\s"
set @m = RegExMatch(@s, @p, 0, "IgnoreCase")
set @o = replace(@s, @m, " ")
OUTPUTLINE(concat("string: ", @s))
OUTPUTLINE(concat('pattern: "', @p, '"'))
OUTPUTLINE(concat("match: ", @m))
OUTPUTLINE(concat("replaced: ", @o))

OUTPUTLINE(concat("Retrieve accessToken value from REST response"))
set @s = '{"accessToken": "e68d15020fc64547b3de7a54bf82e40a", "expiresIn": 3600}'
set @p = '^{"accessToken": "(.*)",.*$'
set @o = RegExMatch(@s, @p, 1)
OUTPUTLINE(concat("string: ", @s))
OUTPUTLINE(concat("pattern: '", @p, "'"))
OUTPUTLINE(concat("output: ", @o))
]%%
++++++++++
Strip leading zeroes from a string
string: 0000012345
pattern: "^0*(\d+)$"
output: 12345
Check for all digits, no match
string: 12345x
pattern: "^\d*$"
output: 
Check for all digits, match
string: 12345
pattern: "^\d*$"
output: 12345
Replace parenthetical text with space
string: whee (whatever it is) whoop
pattern: "\s\(.+\)\s"
match:  (whatever it is) 
replaced: whee whoop
Retrieve accessToken value from REST response
string: {"accessToken": "e68d15020fc64547b3de7a54bf82e40a", "expiresIn": 3600}
pattern: '^{"accessToken": "(.*)",.*$'
output: e68d15020fc64547b3de7a54bf82e40a